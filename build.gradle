plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.32.0"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.27.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.12.2'

    testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '27.1-jre'

    // Remove once Mockito 3.x gets released with Junit 5 support
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.27.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // args "--reporter=checkstyle, output=${buildDir}/ktlint.xml
}
